apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion rootProject.ext.sdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.sdkVersion
    }
    namespace 'com.github.chrisbanes.photoview'

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.6.1"
}

publishing {
    def versionName = "2.3.2"
    publications {
        release(MavenPublication) {
            groupId = 'top.mankin'
            artifactId = 'photoview'
            version = versionName

            afterEvaluate {
                from components.release
            }

            pom {
                name = 'PhotoView'
                description = 'A simple ImageView that support zooming, both by Multi-touch gestures and double-tap.'
                url = 'https://github.com/MankinChung/PhotoView'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'MankinChung'
                    }
                }
                scm {
                    connection = 'scm:git@github.com/MankinChung/PhotoView.git'
                    developerConnection = 'scm:git@github.com/MankinChung/PhotoView.git'
                    url = 'https://github.com/MankinChung/PhotoView'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/content/repositories/releases/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = versionName.endsWith('-SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username mavenUserName
                password mavenPassword
            }
        }
    }
//    repositories {
//        maven {
//            name = "GitHubPackages"
//            url = uri("https://maven.pkg.github.com/$userName/PhotoView")
//            credentials {
//                username = userName
//                password = System.getenv("TOKEN")
//            }
//        }
//    }
}
